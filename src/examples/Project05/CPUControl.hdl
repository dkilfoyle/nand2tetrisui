
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM == 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to restart the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M?
        addressM[15],    // Address in data memory (of M)
        pc[15];          // Address of next instruction

    PARTS:
    // c instruction
    // 1  xx  a   c1 c2 c3 c4 c5 c6   d1 d2 d3   j1 j2 j3
    // 15    12   11 10 09 08 07 06   05 04 03   02 01 00

    // d1 d2 d3
    //  A  D  M
    //  0  0  0  null
    //  0  0  1  M      Memory[A]
    //  0  1  0  D      D
    //  0  1  1  MD     Memory[A] and D
    //  1  0  0  A
    //  1  0  1  AM     A register and Memory[A]
    //  1  1  0  AD     A register and D register
    //  1  1  1  AMD    A register and Memory[A] and D register

    // ALU: a=0 then x=D, y=A
    // ALU: a=1 then x=D, y=M
    //       zx nx zy ny f  no
    // a=0   c1 c2 c3 c4 c5 c6   a=1
    // 0      1  0  1  0  1  0          0+0
    // 1      1  1  1  1  1  1          !(111111+11111=111110)=0000001
    // -1     1  1  1  0  1  0          111111 + 0 = 111111
    // D      0  0  1  1  0  0          D & 111111 = D          
    // A      1  1  0  0  0  0          11111 & A = A
    // !D     0  0  1  1  0  1          !(D & 1111 = D)   
    // !A     1  1  0  0  0  1     
    // -D     0  0  1  1  1  1          !(D + 1111111)
    // -A     1  1  0  0  1  1          !(D + 1111111)
    // D+1    0  1  1  1  1  1          !(!D + -1)
    // A+1    1  1  0  1  1  1
    // D-1    0  0  1  1  1  0
    // A-1    1  1  0  0  1  0
    // D+A    0  0  0  0  1  0
    // D-A    0  1  0  0  1  1          !(!D + A)     D-A = D + -A = D + (!A+1)
    // A-D    0  0  0  1  1  1 

    Controller(instruction=instruction, zr=zr, ng=ng, 
      writeM=writeM,
      loadA=loadA,
      loadD=loadD,
      loadPC=loadPC,
      selA=selA,
      selY=selY);
   
    // A register
    // = instruction if A-instruction (instruction[15]==0)
    // or ALUOut if destination A (d1==T)
    // Mux instr[15] (is C instruction) ? ALU : instruction
    Mux16(a=instruction, b=outtM, sel=selA, out=ALUorAInstr);

    ARegister(in=ALUorAInstr, load=loadA, out=A, out[0..14]=addressM);

    // D register = ALUOut if destination D (d2==T)
    DRegister(in=ALUOut, load=loadD, out=D);

    // ALU y = instruction[12](a) ? inM : A 
    Mux16(a=A, b=inM, sel=selY, out=AM);
 
    ALU(x=D, y=AM, 
      zx=instruction[11],
      nx=instruction[10],
      zy=instruction[9],
      ny=instruction[8],
      f=instruction[7],
      no=instruction[6],
      out=ALUOut, out=outM, out=outtM,
      zr=zr, ng=ng);

    PC(in=A, inc=true, load=loadPC, reset=reset, out[0..14]=pc);
}